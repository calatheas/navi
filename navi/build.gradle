plugins {
	id 'org.springframework.boot' version '2.5.4'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	// rest docs
	id "org.asciidoctor.jvm.convert" version "3.3.2"
}

group = 'com.calathea'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

configurations {
	asciidoctorExtensions
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-redis-reactive'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-aop'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
	implementation 'org.apache.commons:commons-lang3'
	implementation 'software.amazon.awssdk:ses:2.15.0'

	//redis
	implementation 'org.redisson:redisson-spring-boot-starter:3.16.3'

	// rest docs
	asciidoctorExtensions 'org.springframework.restdocs:spring-restdocs-asciidoctor'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
}

// rest docs
ext {
	snippetsDir = file('build/generated-snippets')
}

test {
	// rest docs
	outputs.dir snippetsDir

	useJUnitPlatform()
}

asciidoctor {
	configurations "asciidoctorExtensions" // 이거 없으니까 adoc에서 include 할때 에러남
	inputs.dir snippetsDir
	dependsOn test
}

bootJar {
	dependsOn asciidoctor
	print "asciidoctor.outputDir : ${asciidoctor.outputDir}+(${asciidoctor.outputDir.getClass()})"
	from ("${asciidoctor.outputDir}") {
		into 'static/docs'
	}
}